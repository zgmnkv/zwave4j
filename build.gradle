apply plugin: "java"
apply plugin: "maven"
apply plugin: "application"
apply plugin: "idea"
apply plugin: "cpp"
apply plugin: "c"
apply plugin: "signing"

ext {
    release = project.hasProperty("release") ? Boolean.valueOf(project.release) : false
    signEnabled = project.hasProperty("signEnabled") ? Boolean.valueOf(project.signEnabled) : false
    sonatypeUsername = project.hasProperty("sonatypeUsername") ? project.sonatypeUsername : ""
    sonatypePassword = project.hasProperty("sonatypePassword") ? project.sonatypePassword : ""

    openZWaveDir = project.openZWaveDir
    openZWaveConfigDir = project.openZWaveConfigDir
    zWaveControllerPort = project.zWaveControllerPort

    nativeClasses = ["org.zwave4j.Manager", "org.zwave4j.Options"]

    additionalNativeLibsDir = "native_libs"

    classesNativeLibsDir = "native_libs"
}

group = "com.github.zgmnkv"
version = "0.3" + (release ? "" : "-SNAPSHOT")
mainClassName = "org.zwave4j.Main"

repositories {
    mavenLocal()
    mavenCentral()
}

model {
    platforms {
        windows_x86 {
            architecture "x86"
            operatingSystem "windows"
        }
        windows_amd64 {
            architecture "amd64"
            operatingSystem "windows"
        }
        linux_x86 {
            architecture "x86"
            operatingSystem "linux"
        }
        linux_amd64 {
            architecture "amd64"
            operatingSystem "linux"
        }
        linux_arm {
            architecture "arm"
            operatingSystem "linux"
        }
        osx_x86 {
            architecture "x86"
            operatingSystem "osx"
        }
        osx_amd64 {
            architecture "amd64"
            operatingSystem "osx"
        }
    }
    toolChains {
        gcc(Gcc) {
            //gradle 1.12 for some reason does not support windows amd64 cross compile
            addPlatformConfiguration(new TargetPlatformConfiguration() {
                boolean supportsPlatform(Platform platform) {
                    platform.operatingSystem.current && platform.operatingSystem.name == "windows" &&
                            platform.architecture.name == "amd64"
                }
                List<String> getCppCompilerArgs() { ["-m64"] }
                List<String> getCCompilerArgs() { ["-m64"] }
                List<String> getObjectiveCCompilerArgs() { ["-m64"] }
                List<String> getObjectiveCppCompilerArgs() { ["-m64"] }
                List<String> getAssemblerArgs() { ["--64"] }
                List<String> getLinkerArgs() { ["-m64"] }
                List<String> getStaticLibraryArchiverArgs() { [] }
            })
            //linux arm target support for linux arm host, no compiler/linker options
            addPlatformConfiguration(new TargetPlatformConfiguration() {
                boolean supportsPlatform(Platform platform) {
                    platform.operatingSystem.current && platform.operatingSystem.name == "linux" &&
                            System.properties["os.arch"] == "arm" && platform.architecture.name == "arm"
                }
                List<String> getCppCompilerArgs() { [] }
                List<String> getCCompilerArgs() { [] }
                List<String> getObjectiveCCompilerArgs() { [] }
                List<String> getObjectiveCppCompilerArgs() { [] }
                List<String> getAssemblerArgs() { [] }
                List<String> getLinkerArgs() { [] }
                List<String> getStaticLibraryArchiverArgs() { [] }
            })
        }
        clang(Clang)
    }
}

sources {
    main {
        cpp {
            source {
                //tinyxml sources
                srcDir "$openZWaveDir/cpp/tinyxml"

                //hidapi sources
                if (org.gradle.internal.os.OperatingSystem.current().windows) {
                    srcDir "$openZWaveDir/cpp/hidapi/windows"
                }

                //open-zwave sources
                srcDir "$openZWaveDir/cpp/src"
                srcDir "$openZWaveDir/cpp/src/command_classes"
                srcDir "$openZWaveDir/cpp/src/value_classes"
                srcDir "$openZWaveDir/cpp/src/platform"

                if (org.gradle.internal.os.OperatingSystem.current().windows) {
                    srcDir "$openZWaveDir/cpp/src/platform/windows"
                } else {
                    srcDir "$openZWaveDir/cpp/src/platform/unix"
                }

                //open-zwave version generated sources
                //srcDir "$project.buildDir/ozw-version"

                //zwave4j sources
                srcDir "src/main/cpp"

                include "*.cpp"
            }
        }
        c {
            source {
                if (org.gradle.internal.os.OperatingSystem.current().linux) {
                    srcDir "$openZWaveDir/cpp/hidapi/linux"
                } else if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
                    srcDir "$openZWaveDir/cpp/hidapi/mac"
                }

                srcDir "$openZWaveDir/cpp/src/aes"
                include "*.c"
            }
        }
    }
}

libraries {
    main {
        baseName = "zwave4j"
        binaries.all {
            if (toolChain in Gcc || toolChain in Clang) {
                //platform-specific compiler args
                if (targetPlatform.operatingSystem.windows) {
                    cppCompiler.args "-DMINGW"
                } else if (targetPlatform.operatingSystem.macOsX) {
                    cppCompiler.args "-DDARWIN"
                    cppCompiler.args "-stdlib=libstdc++"
                }

                //tinyxml headers
                cppCompiler.args "-I", "$openZWaveDir/cpp/tinyxml"

                //hidapi headers
                cppCompiler.args "-I", "$openZWaveDir/cpp/hidapi/hidapi"
                cCompiler.args "-I", "$openZWaveDir/cpp/hidapi/hidapi"

                //open-zwave headers
                cppCompiler.args "-I", "$openZWaveDir/cpp/src"
                cppCompiler.args "-I", "$openZWaveDir/cpp/src/command_classes"
                cppCompiler.args "-I", "$openZWaveDir/cpp/src/value_classes"
                cppCompiler.args "-I", "$openZWaveDir/cpp/src/platform"

                if (targetPlatform.operatingSystem.windows) {
                    cppCompiler.args "-I", "$openZWaveDir/cpp/src/platform/windows"
                } else {
                    cppCompiler.args "-I", "$openZWaveDir/cpp/src/platform/unix"
                }

                //java headers
                cppCompiler.args "-I", "${org.gradle.internal.jvm.Jvm.current().javaHome}/include"

                if (targetPlatform.operatingSystem.windows) {
                    cppCompiler.args "-I", "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/win32"
                } else if (targetPlatform.operatingSystem.linux) {
                    cppCompiler.args "-I", "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/linux"
                } else if (targetPlatform.operatingSystem.macOsX) {
                    cppCompiler.args "-I", "${org.gradle.internal.jvm.Jvm.current().javaHome}/include/darwin"
                }

                //zwave4j generated headers
                cppCompiler.args "-I", "$project.buildDir/native-headers"

                //platform-specific linker args
                if (targetPlatform.operatingSystem.windows) {
                    linker.args "-static" //static link mingw dlls (libstdc++, libgcc, libwinpthread, etc)
                    linker.args "-lsetupapi"
                    if (targetPlatform.architecture.name == "x86") {
                        linker.args "-Wl,--kill-at"
                    }
                } else if (targetPlatform.operatingSystem.macOsX) {
                    linker.args "-mmacosx-version-min=10.4"
                    linker.args "-stdlib=libstdc++"
                    linker.args "-framework", "IOKit", "-framework", "CoreFoundation"   
                }

                tasks.withType(CppCompile) {
                    dependsOn generateNativeHeaders, generateOzwVersionCpp
                }
            }
        }
    }
}

task generateOzwVersionCpp {
    outputs.file("$project.buildDir/ozw-version/version.cpp")
    doLast {
        def f = file("$project.buildDir/ozw-version/version.cpp")
        f.parentFile.mkdirs()
        f.text = "" +
                "unsigned short ozw_vers_minor = 1;\n" +
                "unsigned short ozw_vers_major = 0;\n" +
                "unsigned short ozw_vers_revision = 0;\n" +
                "char ozw_vers[] = \"OpenZWave version 1.0.0\";\n"
    }
}

task generateNativeHeaders(type: Exec, dependsOn: classes) {
    inputs.dir(sourceSets.main.output.classesDir)
    outputs.dir("$project.buildDir/native-headers")

    commandLine "javah"
    args "-d", "$project.buildDir/native-headers"
    args "-classpath", sourceSets.main.runtimeClasspath.asPath
    args nativeClasses
}

task copyNativeLibs(type: Copy) {
    into "$sourceSets.main.output.classesDir/$classesNativeLibsDir"
    from additionalNativeLibsDir
}

libraries.main.binaries.withType(SharedLibraryBinary) {
    afterEvaluate {
        //for "arm" host gradle treats "amd64" and "x86" as buildable, adding additional conditions
        if (buildable && (System.properties["os.arch"] != "arm" || targetPlatform.architecture.name == "arm")) {
            copyNativeLibs.dependsOn tasks.builder
            copyNativeLibs.from tasks.builder.outputFile, {
                into "$targetPlatform.operatingSystem.name/$targetPlatform.architecture.name"
            }
        }
    }
}

jar.dependsOn copyNativeLibs

run {
    dependsOn copyNativeLibs
    standardInput = System.in
    jvmArgs([
            "-Xdebug",
            "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=5005"
    ])
    args([
            openZWaveConfigDir,
            zWaveControllerPort
    ])
}

task sourcesJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier "javadoc"
    from javadoc
}

task configZip(type: Zip) {
    from openZWaveConfigDir
    classifier "ozw_config"
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.12"
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives configZip
}

signing {
    required { signEnabled }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { signing.signPom(it) }

            repository(url: "https://oss.sonatype.org/" + (release ? "service/local/staging/deploy/maven2" : "content/repositories/snapshots")) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                packaging "jar"
                name "ZWave4J"
                description "ZWave4J is a Java wrapper for Open-ZWave library"
                url "https://github.com/zgmnkv/zwave4j"

                scm {
                    url "https://github.com/zgmnkv/zwave4j"
                    connection "scm:git:https://github.com/zgmnkv/zwave4j"
                    developerConnection "scm:git:https://github.com/zgmnkv/zwave4j"
                }

                licenses {
                    license {
                        name "GNU GENERAL PUBLIC LICENSE, Version 3, 29 June 2007"
                        url "http://www.gnu.org/licenses/gpl-3.0.txt"
                        distribution "repo"
                    }
                }

                developers {
                    developer {
                        id "zgmnkv"
                        name "Alexander Zagumennikov"
                    }
                }
            }
        }
    }
}
